apply plugin: 'com.android.application'

android {

    compileSdkVersion 26

    defaultConfig {
        applicationId "com.carto.maps.app.droid"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 64
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a','armeabi','x86','arm64-v8a','mips'
            universalApk false
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

}

// map for the version code
ext.versionCodes = ['armeabi':0, 'armeabi-v7a':1, 'arm64-v8a':2, 'mips':3, 'x86':4]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    // assign different version code for each output
    variant.outputs.each { output ->
        output.versionCodeOverride =
                project.ext.versionCodes.get(output.getFilter(OutputFile.ABI)) + android.defaultConfig.versionCode
    }
}

dependencies {
    compile project(':nuticomponents')
    compile 'com.carto:carto-mobile-sdk:4.1.2@aar'
    compile 'com.firebase:firebase-client-android:2.5.0+'
    implementation 'com.android.support:appcompat-v7:26.1.0'
}
